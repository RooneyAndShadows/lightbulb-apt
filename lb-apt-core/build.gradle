plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.android'
}

group "$environment.root_namespace"

android {
    namespace "$environment.core_namespace"
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33
        versionName "$environment.version"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$versions.android.desugar"
    implementation "androidx.appcompat:appcompat:$versions.android.appcompat"
    implementation "androidx.preference:preference-ktx:$versions.android.preference"
    implementation "com.google.code.gson:gson:$versions.google.gson"
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:unchecked"
    options.deprecation = true
}

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        repositories {
            mavenLocal()
        }
        publications {
            release(MavenPublication) {
                from components.release
                groupId group
                version android.defaultConfig.versionName
                artifactId project.name
                artifact androidSourcesJar
            }
        }
    }
}